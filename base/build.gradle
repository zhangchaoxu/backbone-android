apply plugin: 'com.android.library'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'me.tatarka.retrolambda'
// maven
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "0.0.8"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize "4g" //specify the heap size for the dex process
        preDexLibraries = false //delete the already predexed libraries
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 8
        versionName version

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def support_version = "25.3.1"
def butterknife_version = "8.8.0"

dependencies {
    // support
    compile "com.android.support:appcompat-v7:${support_version}"
    compile "com.android.support:recyclerview-v7:${support_version}"
    compile "com.android.support:cardview-v7:${support_version}"
    compile "com.android.support:design:${support_version}"
    compile "com.android.support:percent:${support_version}"
    // multidex
    compile 'com.android.support:multidex:1.0.1'
    // guava
    compile 'com.google.guava:guava:22.0-android'

    // view
    // ViewBinding https://github.com/JakeWharton/butterknife
    compile "com.jakewharton:butterknife:${butterknife_version}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknife_version}"
    // glide https://github.com/bumptech/glide
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    // auto layout https://github.com/hongyangAndroid/AndroidAutoLayout
    compile 'com.zhy:autolayout:1.4.5'
    // circleimageview https://github.com/hdodenhof/CircleImageView
    compile 'de.hdodenhof:circleimageview:2.1.0'
    // recyclerview https://github.com/CymChad/BaseRecyclerViewAdapterHelper
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.27'
    // FloatingActionButton https://github.com/Clans/FloatingActionButton
    // compile 'com.github.clans:fab:1.6.4'
    // statusbar https://github.com/laobie/StatusBarUtil
    compile 'com.jaeger.statusbarutil:library:1.4.0'
    // okhttp3 https://github.com/square/okhttp
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.1'

    // retrofit2 https://github.com/square/retrofit
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    // rxjava https://github.com/ReactiveX/RxJava
    compile "io.reactivex.rxjava2:rxjava:2.1.1"
    // rxandroid https://github.com/ReactiveX/rxandroid
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // rxlifecycle
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-android:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.0.1'
    // json https://github.com/google/gson
    compile 'com.google.code.gson:gson:2.8.0'
    // event https://github.com/greenrobot/EventBus
    compile 'org.greenrobot:eventbus:3.0.0'

    // permission https://github.com/yanzhenjie/AndPermission
    compile 'com.yanzhenjie:permission:1.1.0'

    // dialogs https://github.com/afollestad/material-dialogs
    compile 'com.afollestad.material-dialogs:core:0.9.4.5'
    // TabLayout https://github.com/H07000223/FlycoTabLayout
    compile 'com.flyco.tablayout:FlycoTabLayout_Lib:2.1.2@aar'
    // update https://github.com/czy1121/update
    compile 'com.github.zhangchaoxu:update:1.1.2'

    // logger https://github.com/orhanobut/logger
    compile 'com.orhanobut:logger:1.15'

    // debug
    // stetho https://github.com/facebook/stetho
    compile 'com.facebook.stetho:stetho:1.5.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    testCompile 'junit:junit:4.12'
}

def siteUrl = 'https://github.com/zhangchaoxu/backbone-android'
def gitUrl = 'https://github.com/zhangchaoxu/backbone-android.git'
// group name do not need match package name
// will be display in the compile path
def artifactId = 'base'
group = 'com.idogfooding.backbone'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name artifactId
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id properties.getProperty("developer.id")
                        name properties.getProperty("developer.name")
                        email properties.getProperty("developer.email")
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options {
        encoding = "UTF-8"
        charSet "UTF-8"
    }
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    //archives javadocJar
    archives sourcesJar
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = artifactId
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

